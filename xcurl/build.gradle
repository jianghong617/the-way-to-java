plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jetbrains.intellij' version '1.9.0'
    id "com.github.hierynomus.license" version "0.16.1"
}

group 'com.photowey'
version '1.0.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

// Include the generated files in the source set
sourceSets {
    main {
        java {
            srcDirs("src/main/gen")
        }
    }
}

dependencies {
    implementation 'com.alibaba.fastjson2:fastjson2:2.0.19'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.3.2'
    plugins = ['com.intellij.java']
}

// ----------------------------------------------------------------

patchPluginXml {
    version = "${project.version}"
    sinceBuild.set("213")
    untilBuild.set("222.*")
}

license {
    header rootProject.file('codequality/HEADER')
    excludes(["**/*.png", "**/*.svg", "**/*.xml", "**/*.gradle", "**/*.md", "**/gradlew.bat", "**/gradlew", "**/package-info.java", "**/gen/**/*.java"])
    strictCheck true
}

test {
    useJUnitPlatform()
}

task cleanDist(type: Delete) {
    group("build")

    delete("dist")
}

// ----------------------------------------------------------------

buildPlugin {
    buildSearchableOptions {
        enabled = false
    }
}

// ----------------------------------------------------------------

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}